#
#  CMake custom modules
#  Copyright (C) 2011-2015  Cedric OCHS
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

FIND_PACKAGE_HELPER(Poppler poppler-config.h SUFFIXES poppler QUIET)

FOREACH(COMPONENT ${Poppler_FIND_COMPONENTS})
  IF(COMPONENT STREQUAL "poppler-cpp")
    FIND_PACKAGE_HELPER(PopplerCpp poppler-version.h RELEASE poppler-cpp DEBUG poppler-cppd SUFFIXES poppler/cpp DIR ${POPPLER_DIR} QUIET)
    IF(POPPLERCPP_FOUND)
      SET(POPPLER_FOUND ON)
    ENDIF()
  ELSEIF(COMPONENT STREQUAL "poppler-glib")
    FIND_PACKAGE_HELPER(PopplerGlib poppler.h RELEASE poppler-glib DEBUG poppler-glibd SUFFIXES poppler/glib DIR ${POPPLER_DIR} QUIET)
    IF(POPPLERGLIB_FOUND)
      SET(POPPLER_FOUND ON)
    ENDIF()
  ELSEIF(COMPONENT STREQUAL "poppler-qt4")
    FIND_PACKAGE_HELPER(PopplerQt4 poppler-qt4.h RELEASE poppler-qt4 DEBUG poppler-qt4d SUFFIXES poppler/qt4 DIR ${POPPLER_DIR} QUIET)
    IF(POPPLERQT4_FOUND)
      SET(POPPLER_FOUND ON)
    ENDIF()
  ELSEIF(COMPONENT STREQUAL "poppler-qt5")
    FIND_PACKAGE_HELPER(PopplerQt5 poppler-qt5.h RELEASE poppler-qt5 DEBUG poppler-qt5d SUFFIXES poppler/qt5 DIR ${POPPLER_DIR} QUIET)
    IF(POPPLERQT5_FOUND)
      SET(POPPLER_FOUND ON)
    ENDIF()
  ENDIF()
ENDFOREACH()

IF(POPPLER_FOUND)
  # Also include parent directory
  GET_FILENAME_COMPONENT(POPPLER_INCLUDE_ROOT_DIR "${POPPLER_INCLUDE_DIR}/.." ABSOLUTE)

  IF(CMAKE_VERSION VERSION_GREATER "2.8.9")
    # Directory where to search all dependencies
    GET_FILENAME_COMPONENT(POPPLER_LIBRARY_DIR "${POPPLER_LIBRARY_RELEASE}" DIRECTORY)
  ELSE()
    # For old versions of Linux
    # Directory where to search all dependencies
    GET_FILENAME_COMPONENT(POPPLER_LIBRARY_DIR "${POPPLER_INCLUDE_DIR}/../../lib${LIB_SUFFIX}" ABSOLUTE)
  ENDIF()

  # don't use FIND_PACKAGE because we don't need development files
  FIND_LIBRARY_HELPER(JPEG DIR ${POPPLER_DIR})
  FIND_LIBRARY_HELPER(ZLIB RELEASE z DEBUG zd DIR ${POPPLER_DIR})
  FIND_LIBRARY_HELPER(PNG RELEASE libpng16_static DEBUG libpng16_staticd DIR ${POPPLER_DIR})
  FIND_LIBRARY_HELPER(FREETYPE DIR ${POPPLER_DIR})
  FIND_LIBRARY_HELPER(OPENJPEG RELEASE openjp2 DEBUG openjp2d DIR ${POPPLER_DIR})

  SET(POPPLER_INCLUDE_DIRS ${POPPLER_INCLUDE_DIRS} ${POPPLER_INCLUDE_DIR} ${POPPLER_INCLUDE_ROOT_DIR})
  SET(POPPLER_LIBRARIES ${POPPLER_LIBRARIES}
    ${JPEG_LIBRARIES}
    ${ZLIB_LIBRARIES}
    ${PNG_LIBRARIES}
    ${FREETYPE_LIBRARIES}
    ${OPENJPEG_LIBRARIES}
  )

  PARSE_VERSION_OTHER(${POPPLER_INCLUDE_DIR}/poppler-config.h POPPLER_VERSION)

  IF(POPPLER_VERSION)
    PARSE_VERSION_STRING(${POPPLER_VERSION} POPPLER_VERSION_MAJOR POPPLER_VERSION_MINOR POPPLER_VERSION_PATCH)
  ELSE(POPPLER_VERSION)
    PARSE_VERSION_OTHER(${POPPLER_INCLUDE_DIR}/glib/poppler-features.h POPPLER_MAJOR_VERSION POPPLER_MINOR_VERSION POPPLER_MICRO_VERSION)

    IF(DEFINED POPPLER_MAJOR_VERSION)
      SET(POPPLER_VERSION_MAJOR ${POPPLER_MAJOR_VERSION})
      SET(POPPLER_VERSION_MINOR ${POPPLER_MINOR_VERSION})
      SET(POPPLER_VERSION_PATCH ${POPPLER_MICRO_VERSION})
      SET(POPPLER_VERSION "${POPPLER_VERSION_MAJOR}.${POPPLER_VERSION_MINOR}.${POPPLER_VERSION_PATCH}")
    ELSE()
      SET(POPPLER_VERSION_MAJOR 0)
      SET(POPPLER_VERSION_MINOR 10)
      SET(POPPLER_VERSION_PATCH 0)
      SET(POPPLER_VERSION "0.10.0")
    ENDIF()
  ENDIF()

  CONVERT_VERSION_NUMBER(POPPLER_VERSION_NUM 100 ${POPPLER_VERSION_MAJOR} ${POPPLER_VERSION_MINOR} ${POPPLER_VERSION_PATCH})
  SET(POPPLER_DEFINITIONS "-DPOPPLER_VERSION_NUM=${POPPLER_VERSION_NUM}")

  MESSAGE_VERSION_PACKAGE_HELPER(Poppler ${POPPLER_VERSION} ${POPPLER_LIBRARIES})
ENDIF()

IF(POPPLERGLIB_FOUND)
  SET(POPPLER_INCLUDE_DIRS ${POPPLER_INCLUDE_DIRS} ${POPPLERGLIB_INCLUDE_DIR})
  SET(POPPLER_LIBRARIES ${POPPLER_LIBRARIES} ${POPPLERGLIB_LIBRARIES})

  MESSAGE_VERSION_PACKAGE_HELPER(PopplerGlib ${POPPLER_VERSION} ${POPPLERGLIB_LIBRARIES})
ENDIF()

IF(POPPLERQT4_FOUND)
  SET(POPPLER_INCLUDE_DIRS ${POPPLER_INCLUDE_DIRS} ${POPPLERQT4_INCLUDE_DIR})
  SET(POPPLER_LIBRARIES ${POPPLER_LIBRARIES} ${POPPLERQT4_LIBRARIES})

  MESSAGE_VERSION_PACKAGE_HELPER(PopplerQt4 ${POPPLER_VERSION} ${POPPLERQT4_LIBRARIES})
ENDIF()

IF(POPPLERQT5_FOUND)
  SET(POPPLER_INCLUDE_DIRS ${POPPLER_INCLUDE_DIRS} ${POPPLERQT5_INCLUDE_DIR})
  SET(POPPLER_LIBRARIES ${POPPLER_LIBRARIES} ${POPPLERQT5_LIBRARIES})

  MESSAGE_VERSION_PACKAGE_HELPER(PopplerQt5 ${POPPLER_VERSION} ${POPPLERQT5_LIBRARIES})
ENDIF()
