#
#  CMake custom modules
#  Copyright (C) 2011-2015  Cedric OCHS
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

IF(DEFINED CMAKE_CROSSCOMPILING)
  # subsequent toolchain loading is not really needed
  RETURN()
ENDIF()

# Standard settings
SET(CMAKE_SYSTEM_NAME Linux)
SET(CMAKE_SYSTEM_VERSION 1) # TODO: determine target Linux version
SET(UNIX ON)
SET(LINUX ON)
SET(ANDROID ON)

IF(NOT ANDROID_NDK_HOME)
  SET(ANDROID_NDK_HOME $ENV{ANDROID_NDK_HOME})

  IF(CMAKE_HOST_WIN32)
    FILE(TO_CMAKE_PATH ${ANDROID_NDK_HOME} ANDROID_NDK_HOME)
  ENDIF()
ENDIF()

IF(NOT TARGET_CPU)
  SET(TARGET_CPU "armv7")
ENDIF()

IF(TARGET_CPU STREQUAL "armv7")
  SET(LIBRARY_ARCHITECTURE "armeabi-v7a")
  SET(CMAKE_SYSTEM_PROCESSOR "armv7")
  SET(TOOLCHAIN_ARCH "arm")
  SET(GCC_TOOLCHAIN_PREFIX "arm-linux-androideabi")
  SET(TOOLCHAIN_BIN_PREFIX "arm-linux-androideabi")
  SET(MINIMUM_NDK_TARGET 14)
ELSEIF(TARGET_CPU STREQUAL "armv5")
  SET(LIBRARY_ARCHITECTURE "armeabi")
  SET(CMAKE_SYSTEM_PROCESSOR "armv5")
  SET(TOOLCHAIN_ARCH "arm")
  SET(GCC_TOOLCHAIN_PREFIX "arm-linux-androideabi")
  SET(TOOLCHAIN_BIN_PREFIX "arm-linux-androideabi")
  SET(MINIMUM_NDK_TARGET 14)
ELSEIF(TARGET_CPU STREQUAL "arm64")
  SET(LIBRARY_ARCHITECTURE "arm64-v8a")
  SET(CMAKE_SYSTEM_PROCESSOR "arm64")
  SET(TOOLCHAIN_ARCH "arm64")
  SET(GCC_TOOLCHAIN_PREFIX "aarch64-linux-android")
  SET(TOOLCHAIN_BIN_PREFIX "aarch64-linux-android")
  SET(MINIMUM_NDK_TARGET 21)
ELSEIF(TARGET_CPU STREQUAL "x86")
  SET(LIBRARY_ARCHITECTURE "x86")
  SET(CMAKE_SYSTEM_PROCESSOR "x86")
  SET(TOOLCHAIN_ARCH "x86")
  SET(GCC_TOOLCHAIN_PREFIX "x86")
  SET(TOOLCHAIN_BIN_PREFIX "i686-linux-android")
  SET(MINIMUM_NDK_TARGET 14)
ELSEIF(TARGET_CPU STREQUAL "x86_64")
  SET(LIBRARY_ARCHITECTURE "x86_64")
  SET(CMAKE_SYSTEM_PROCESSOR "x86_64")
  SET(TOOLCHAIN_ARCH "x86_64")
  SET(GCC_TOOLCHAIN_PREFIX "x86_64")
  SET(TOOLCHAIN_BIN_PREFIX "x86_64-linux-android")
  SET(MINIMUM_NDK_TARGET 21)
ELSEIF(TARGET_CPU STREQUAL "mips")
  SET(LIBRARY_ARCHITECTURE "mips")
  SET(CMAKE_SYSTEM_PROCESSOR "mips")
  SET(TOOLCHAIN_ARCH "mips")
  SET(GCC_TOOLCHAIN_PREFIX "mipsel-linux-android")
  SET(TOOLCHAIN_BIN_PREFIX "mipsel-linux-android")
  SET(MINIMUM_NDK_TARGET 14)
ELSEIF(TARGET_CPU STREQUAL "mips64")
  SET(LIBRARY_ARCHITECTURE "mips64")
  SET(CMAKE_SYSTEM_PROCESSOR "mips64")
  SET(TOOLCHAIN_ARCH "mips64")
  SET(GCC_TOOLCHAIN_PREFIX "mips64el-linux-android")
  SET(TOOLCHAIN_BIN_PREFIX "mips64el-linux-android")
  SET(MINIMUM_NDK_TARGET 21)
ELSE()
  MESSAGE(FATAL_ERROR "Unable to process TARGET_CPU ${TARGET_CPU}")
ENDIF()

SET(CLANG_TOOLCHAIN_PREFIX "llvm")
SET(ANDROID_COMPILER "clang")
SET(ANDROID_COMPILER "clang")
SET(CLANG ON)

IF(NOT NDK_TARGET)
  SET(NDK_TARGET ${MINIMUM_NDK_TARGET})
ELSE()
  IF(NDK_TARGET LESS MINIMUM_NDK_TARGET)
    SET(NDK_TARGET ${MINIMUM_NDK_TARGET})
  ENDIF()
ENDIF()

IF(CMAKE_HOST_WIN32)
  SET(TOOLCHAIN_HOST "windows-x86_64")
  SET(TOOLCHAIN_BIN_SUFFIX ".exe")
ELSEIF(CMAKE_HOST_APPLE)
  SET(TOOLCHAIN_HOST "apple")
  SET(TOOLCHAIN_BIN_SUFFIX "")
ELSEIF(CMAKE_HOST_UNIX)
  SET(TOOLCHAIN_HOST "linux-x86_64")
  SET(TOOLCHAIN_BIN_SUFFIX "")
ENDIF()

# clang
SET(CLANG_TOOLCHAIN_ROOT "${ANDROID_NDK_HOME}/toolchains/${CLANG_TOOLCHAIN_PREFIX}/prebuilt/${TOOLCHAIN_HOST}")

IF(EXISTS ${CLANG_TOOLCHAIN_ROOT})
  MESSAGE(STATUS "Found LLVM toolchain in ${CLANG_TOOLCHAIN_ROOT}")
ELSE()
  MESSAGE(FATAL_ERROR "No LLVM toolchain found in default search path ${CLANG_TOOLCHAIN_ROOT}")
ENDIF()

# gcc
SET(GCC_TOOLCHAIN_VERSIONS)
FILE(GLOB _TOOLCHAIN_VERSIONS "${ANDROID_NDK_HOME}/toolchains/${GCC_TOOLCHAIN_PREFIX}-*")
IF(_TOOLCHAIN_VERSIONS)
  LIST(SORT _TOOLCHAIN_VERSIONS)
  LIST(REVERSE _TOOLCHAIN_VERSIONS)
  FOREACH(_TOOLCHAIN_VERSION ${_TOOLCHAIN_VERSIONS})
    STRING(REGEX REPLACE ".+${_PREFIX}-([0-9.]+)" "\\1" _TOOLCHAIN_VERSION "${_TOOLCHAIN_VERSION}")
    IF(_TOOLCHAIN_VERSION MATCHES "^([0-9.]+)$")
      LIST(APPEND GCC_TOOLCHAIN_VERSIONS ${_TOOLCHAIN_VERSION})
    ENDIF()
  ENDFOREACH()
ENDIF()

IF(NOT GCC_TOOLCHAIN_VERSIONS)
  MESSAGE(FATAL_ERROR "No GCC version found in default search path ${ANDROID_NDK_HOME}/toolchains")
ENDIF()

IF(GCC_TOOLCHAIN_VERSIONS)
  LIST(FIND GCC_TOOLCHAIN_VERSIONS "${GCC_TOOLCHAIN_VERSION}" _INDEX)
  IF(_INDEX EQUAL -1)
    LIST(GET GCC_TOOLCHAIN_VERSIONS 0 GCC_TOOLCHAIN_VERSION)
  ENDIF()
ELSE()
  LIST(GET GCC_TOOLCHAIN_VERSIONS 0 GCC_TOOLCHAIN_VERSION)
ENDIF()

SET(GCC_TOOLCHAIN_ROOT "${ANDROID_NDK_HOME}/toolchains/${GCC_TOOLCHAIN_PREFIX}-${GCC_TOOLCHAIN_VERSION}/prebuilt/${TOOLCHAIN_HOST}")

IF(NOT EXISTS "${GCC_TOOLCHAIN_ROOT}")
  FILE(GLOB _TOOLCHAIN_PREFIXES "${GCC_TOOLCHAIN_ROOT}*")
  IF(_TOOLCHAIN_PREFIXES)
    LIST(GET _TOOLCHAIN_PREFIXES 0 GCC_TOOLCHAIN_ROOT)
  ENDIF()
ENDIF()

IF(EXISTS "${GCC_TOOLCHAIN_ROOT}")
  MESSAGE(STATUS "Found GCC toolchain in ${GCC_TOOLCHAIN_ROOT}")
ELSE()
  MESSAGE(FATAL_ERROR "No GCC toolchain found in default search path ${GCC_TOOLCHAIN_ROOT}")
ENDIF()

# NDK
SET(PLATFORM_ROOT "${ANDROID_NDK_HOME}/platforms/android-${NDK_TARGET}/arch-${TOOLCHAIN_ARCH}")

MESSAGE(STATUS "Target Android NDK ${NDK_TARGET} found in ${PLATFORM_ROOT}")

# Sysroot.
SET(CMAKE_SYSROOT "${ANDROID_NDK_HOME}/sysroot")
SET(CMAKE_SYSROOT_COMPILE "${CMAKE_SYSROOT}")

# include dirs
SET(PLATFORM_INCLUDE_DIR "${CMAKE_SYSROOT}/usr/include")

IF(NOT EXISTS "${PLATFORM_INCLUDE_DIR}")
  SET(PLATFORM_INCLUDE_DIR "${PLATFORM_ROOT}/usr/include")

  IF(NOT EXISTS "${PLATFORM_INCLUDE_DIR}")
    MESSAGE(FATAL_ERROR "Unable to find platform headers")
  ENDIF()
ENDIF()

SET(STL_DIR "${ANDROID_NDK_HOME}/sources/cxx-stl/gnu-libstdc++")
# ${ANDROID_NDK_HOME}/sources/cxx-stl/llvm-libc++
IF(EXISTS "${STL_DIR}/${GCC_TOOLCHAIN_VERSION}")
  # NDK version >= 8b
  SET(STL_DIR "${STL_DIR}/${GCC_TOOLCHAIN_VERSION}")
ENDIF()

SET(STL_INCLUDE_DIR "${STL_DIR}/include")
SET(STL_LIBRARY_DIR "${STL_DIR}/libs/${LIBRARY_ARCHITECTURE}")
SET(STL_INCLUDE_CPU_DIR "${STL_LIBRARY_DIR}/include")
SET(STL_LIBRARY "${STL_LIBRARY_DIR}/libgnustl_static.a")
# ${STL_LIBRARY_DIR}/libc++_static.a

MESSAGE(STATUS "STL include dir: ${STL_INCLUDE_DIR}")
MESSAGE(STATUS "STL library dir: ${STL_LIBRARY_DIR}")

SET(CMAKE_FIND_ROOT_PATH ${CLANG_TOOLCHAIN_ROOT} ${GCC_TOOLCHAIN_ROOT} ${CMAKE_SYSROOT}/usr ${CMAKE_PREFIX_PATH} ${CMAKE_INSTALL_PREFIX} $ENV{EXTERNAL_ANDROID_PATH} CACHE string  "Android find search path root")

SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM ONLY)
SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)

MACRO(SET_TOOLCHAIN_BINARY_LLVM _NAME _BINARY)
  SET(${_NAME} ${CLANG_TOOLCHAIN_ROOT}/bin/${_BINARY}${TOOLCHAIN_BIN_SUFFIX} CACHE PATH "" FORCE)
ENDMACRO()

MACRO(SET_TOOLCHAIN_BINARY_GCC _NAME _BINARY)
  SET(${_NAME} ${GCC_TOOLCHAIN_ROOT}/bin/${TOOLCHAIN_BIN_PREFIX}-${_BINARY}${TOOLCHAIN_BIN_SUFFIX} CACHE PATH "" FORCE)
ENDMACRO()

SET(CMAKE_TRY_COMPILE_TARGET_TYPE STATIC_LIBRARY)

SET_TOOLCHAIN_BINARY_LLVM(CMAKE_C_COMPILER clang)
SET_TOOLCHAIN_BINARY_LLVM(CMAKE_CXX_COMPILER clang++)

SET(CMAKE_C_COMPILER ${CMAKE_C_COMPILER})
SET(CMAKE_C_COMPILER_TARGET ${TOOLCHAIN_BIN_PREFIX})
SET(CMAKE_C_COMPILER_FORCED TRUE)

SET(CMAKE_CXX_COMPILER ${CMAKE_CXX_COMPILER})
SET(CMAKE_CXX_COMPILER_TARGET ${TOOLCHAIN_BIN_PREFIX})
SET(CMAKE_CXX_COMPILER_FORCED TRUE)

SET_TOOLCHAIN_BINARY_LLVM(CMAKE_ASM_COMPILER llvm-as)
SET_TOOLCHAIN_BINARY_LLVM(CMAKE_AR llvm-ar)
SET_TOOLCHAIN_BINARY_LLVM(CMAKE_LINKER clang++)

IF(NOT EXISTS "${CMAKE_ASM_COMPILER}")
  SET_TOOLCHAIN_BINARY_GCC(CMAKE_ASM_COMPILER as)
ENDIF()

IF(NOT EXISTS "${CMAKE_AR}")
  SET_TOOLCHAIN_BINARY_GCC(CMAKE_AR ar)
ENDIF()

SET_TOOLCHAIN_BINARY_GCC(CMAKE_STRIP strip)
SET_TOOLCHAIN_BINARY_GCC(CMAKE_NM nm)
SET_TOOLCHAIN_BINARY_GCC(CMAKE_OBJCOPY objcopy)
SET_TOOLCHAIN_BINARY_GCC(CMAKE_OBJDUMP objdump)
SET_TOOLCHAIN_BINARY_GCC(CMAKE_RANLIB ranlib)
